start: model

model: load_stmt* class_def*

load_stmt: "load" STRING

class_def: CLASS_TYPE ID ("implements" ID)? (justification_pattern | composition)

justification_pattern: "{" (variable | instruction | support )+ "}"
variable: VARIABLE_TYPE ID instruction
instruction: "is" STRING
support: ID "supports" ID

composition: "{" (composition_variable | composition_instruction )+ "}"
composition_variable: "justification" ID
composition_instruction: composition_variable "is" composition_information
composition_information: ID "with" ID

VARIABLE_TYPE.2: "evidence"
               | "strategy"
               | "sub-conclusion"
               | "conclusion"
               | "@support"

CLASS_TYPE.3: "justification"
            | "pattern"
            | "composition"

STRING: /"(\\.|[^"\\])*"|'(\\.|[^'\\])*'/

ID: /[a-zA-Z_]\w*/


%import common.WS
%ignore WS

SL_COMMENT: /\/\/[^\n\r]*/
ML_COMMENT: /\/\*[\s\S]*?\*\//

%ignore SL_COMMENT
%ignore ML_COMMENT
